# Maintainer: loathingkernel
# Contributor: Torge Matthies <openglfreak at googlemail dot com>
# Contributor: Krzysztof Bogacki <krzysztof dot bogacki at leancode dot pl>
# Contributor: Echo J. <aidas957 at gmail dot com>
# Contributor: Zebra2711

pkgbase=ntsync
pkgname=(ntsync-dkms)
pkgver=6.14
pkgrel=3
pkgdesc="NT synchronization primitive driver"
arch=(any)
url='www.kernel.org'
license=('GPL-2.0-only WITH Linux-syscall-note')
source=("ntsync.c"
        "ntsync.h"
        'ntsync.conf'
        '99-ntsync.rules'
        'Makefile'
        'dkms.conf')
sha256sums=('2fb92d6befb6a67cfba3ccab51bd65bc313994fe5c20c1223d3b79cb74129e45'
            '006437ee52a3e04f921df77081eb5c21c44c71f598b10ac534c6ef9e78296262'
            'c19771ae86e7df179f6b2f4a2837d3f0cbbbba7b32baef41a3c27120c760d78f'
            'ce5221146a19206ba043211db8f27143a82f9224c0aff24a0b584b7268fcb994'
            '834a7b4c9a67a44f2cf593bf259918ea12b0c0eeee7862ed4f9fd268076171cf'
            'e81694fa952711f1b74f02b6a64ac1e90c229f93c740e4f97df5692f3af99609')

prepare() {
    sed -i -e "s/@PACKAGE_VERSION@/$pkgver/g" "$srcdir/dkms.conf"
}

package_ntsync-dkms() {
    depends=(dkms)
    provides=(NTSYNC-MODULE)
    conflicts=(ntsync ntsync-common)

    # Force the use of included header (required since 6.10+)
    sed 's|<uapi/linux/ntsync.h>|"../../include/uapi/linux/ntsync.h"|g' -i "$srcdir/ntsync.c"

    install -Dm644 "$srcdir/Makefile" "$pkgdir/usr/src/$pkgbase/Makefile"
    install -Dm644 "$srcdir/ntsync.h" "$pkgdir/usr/src/$pkgbase/include/uapi/linux/ntsync.h"
    install -Dm644 "$srcdir/ntsync.c" "$pkgdir/usr/src/$pkgbase/drivers/misc/ntsync.c"
    install -Dm644 "$srcdir/dkms.conf" "$pkgdir/usr/src/$pkgbase/dkms.conf"

    install -Dm644 "$srcdir/ntsync.conf" "$pkgdir/usr/lib/modules-load.d/ntsync.conf"

    # fix ntsync: Set the permissions to be 0664
    # install -Dm644 "$srcdir/99-ntsync.rules" "$pkgdir/usr/lib/udev/rules.d/99-ntsync.rules"
}

