# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan Fader <jan.fader@web.de>

pkgname=fish
pkgver=4.0.beta1.r19.gb19a467ea
pkgrel=1
pkgdesc='Smart and user friendly shell intended mostly for interactive use'
url='https://fishshell.com/'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'gcc-libs' 'ncurses' 'pcre2')
optdepends=('python: man page completion parser / web config tool'
            'pkgfile: command-not-found hook')
makedepends=('cargo' 'cmake' 'python-sphinx' 'jq')
checkdepends=('expect' 'procps-ng')
options=(!lto)
provides=(
  'fish'
  'fish-shell'
)
conflicts=(
  'fish'
  'fish-shell'
)
install=fish.install
backup=(etc/fish/config.fish)
source=(${pkgname}::git+https://github.com/fish-shell/fish-shell.git
        'sphinx-no-parallel.patch')
sha256sums=('SKIP'
            '16e15755ab363e1b9f04969091104c88c3cfc39a67621ad636419535e692874f')
prepare() {
  cd ${pkgname}
  patch -Np1 < "$srcdir/sphinx-no-parallel.patch" # can be removed when sphinx 7.3.0 releases
}

pkgver() {
  git -C fish describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^fish.//;s/^v//;s/b/.beta/;'
}

build() {
  cd ${pkgname}
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DBUILD_DOCS=True \
    -Wno-dev
  make -C build
}

package() {
  cd ${pkgname}
  make -C build DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
