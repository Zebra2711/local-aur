# Maintainer: Daniel Bermond <dbermond@archlinux.org>

# Configuration options - set to 1 to enable, 0 to disable
NTSYNC=1         # Enable NTSync support
USE_STAGING_PATCHES=0  # Enable all patches from staging-patches folder

pkgname=wine-staging-tkg
pkgver=10.7.r0.ntsync.g71c6c3dd6dc
pkgrel=1
pkgdesc='A compatibility layer for running Windows programs (staging branch, git version)'
arch=('x86_64')
url='https://www.wine-staging.com/'
license=('LGPL-2.1-or-later')
depends=(
    'attr'             'lib32-attr'
    'fontconfig'       'lib32-fontconfig'
    'libxcursor'       'lib32-libxcursor'
    'libxrandr'        'lib32-libxrandr'
    'libxi'            'lib32-libxi'
    'gettext'          'lib32-gettext'
    'freetype2'        'lib32-freetype2'
    'gcc-libs'         'lib32-gcc-libs'
    'libpcap'          'lib32-libpcap'
    'desktop-file-utils'
)
makedepends=(autoconf bison perl flex mingw-w64-gcc
  git
  giflib                lib32-giflib
  gnutls                lib32-gnutls
  libxinerama           lib32-libxinerama
  libxcomposite         lib32-libxcomposite
  libxxf86vm            lib32-libxxf86vm
  v4l-utils             lib32-v4l-utils
  alsa-lib              lib32-alsa-lib
  libxcomposite         lib32-libxcomposite
  mesa                  lib32-mesa
  mesa-libgl            lib32-mesa-libgl
  opencl-icd-loader     lib32-opencl-icd-loader
  libpulse              lib32-libpulse
  libva                 lib32-libva
  gtk3                  lib32-gtk3
  gst-plugins-base-libs lib32-gst-plugins-base-libs
  vulkan-icd-loader     lib32-vulkan-icd-loader
  sdl3                  lib32-sdl3
  sdl2-compat           lib32-sdl2-compat
  libcups               lib32-libcups
  sane
  libgphoto2
  ffmpeg
  samba
  opencl-headers
)

optdepends=(
    'giflib'                'lib32-giflib'
    'gnutls'                'lib32-gnutls'
    'v4l-utils'             'lib32-v4l-utils'
    'libpulse'              'lib32-libpulse'
    'alsa-plugins'          'lib32-alsa-plugins'
    'alsa-lib'              'lib32-alsa-lib'
    'libxcomposite'         'lib32-libxcomposite'
    'libxinerama'           'lib32-libxinerama'
    'opencl-icd-loader'     'lib32-opencl-icd-loader'
    'opencl-nvidia'         'lib32-opencl-nvidia'
    'libva'                 'lib32-libva'
    'gtk3'                  'lib32-gtk3'
    'gst-plugins-base-libs' 'lib32-gst-plugins-base-libs'
    'vulkan-icd-loader'     'lib32-vulkan-icd-loader'
    'sdl3'                  'lib32-sdl3'
    'sdl2-compat'           'lib32-sdl2-compat'
    'sane'
    'libgphoto2'
    'ffmpeg'
    'cups'                  'lib32-libcups'
    'samba'
    'dosbox'
    'wine-gecko'
    'wine-mono'
)
_tkg_commit=8d83bdd5b502dae7f5f047436ff1f76ff6523251
_wine_commit=71c6c3dd6dcfffc040aa5a10a825eeb8e17f6299
_staging_commit=42a307df6790bdf84f2fd9095a4a4c169a797782
_winehq_mr=https://gitlab.winehq.org/wine/wine/-/merge_requests
options=('staticlibs' '!lto' '!debug')
install="wine.install"
provides=("wine-staging=${pkgver}" "wine=${pkgver%%.r*}" 'wine-wow64')
conflicts=('wine' 'wine-wow64')
source=("git+https://gitlab.winehq.org/wine/wine-staging.git#commit=${_staging_commit}"
        "git+https://gitlab.winehq.org/wine/wine.git#commit=${_wine_commit}"
        '30-win32-aliases.conf'
        'wine-binfmt.conf'
        "git+https://github.com/Frogging-Family/wine-tkg-git.git#commit=${_tkg_commit}"
        'wine-hags.patch'
        'wine-ntsync.patch'
        'wine-wmclass.patch'
        'wine-isolate_home.patch')

# Patch folder where all staging patches will be stored
_patch_dir="mr_patches"

# Merge Request (MR) Patches
# Format: [MR_NUMBER]="Short description of the patch"
declare -gA _mr_patches=(
    [6855]="server: Fix renaming file to a hardlink of itself"
    [7650]="Add support for AF_UNIX sockets (followup)"
    [7848]="server: Use events as a sync for several objects" 
    [7872]="shell32: Avoid writing past end of xlpFile or lpResult in SHELL_FindExecutable()"
    #[7678]="winewayland: Use system cursor shapes when possible"
)

# Create basic checksums array
sha256sums=('SKIP'
            'SKIP'
            '9901a5ee619f24662b241672a7358364617227937d5f6d3126f70528ee5111e7'
            '6dfdefec305024ca11f35ad7536565f5551f09119dda2028f194aee8f77077a4'
            'SKIP'
            '23bd2a1254ec8a0a463bf0ee263377cc77187f56d8a3bd62d6c2b156e8222627'
            'be5440ebea0e15cc9ae260e8c1bb536cf5e8016d9490ac165b005c89ac5dc717'
            'f897dddbeeb27a020ca29eb32ddfa9a0022505aac000d7e5bb4a7053c793c665'
            '959272d91ea6928a14ca821b2d71d543fa9043d3c00a8125f9cdbaeee6289fdb')

# Download MR patches and add to source array
if [[ ${#_mr_patches[@]} -gt 0 ]]; then
    # Create MR patches directory
    mkdir -p "$_patch_dir"

    # Add MR patches to source array
    for mr in "${!_mr_patches[@]}"; do
        source+=("${_patch_dir}/${mr}.patch::${_winehq_mr}/${mr}.patch")
        sha256sums+=('SKIP')
    done
fi

# Add all patches from staging-patches folder if enabled
if [[ "$USE_STAGING_PATCHES" -eq 1 ]] && [[ -d "$srcdir/staging-patches" ]]; then
    shopt -s nullglob
    for patch_file in "$srcdir/staging-patches"/*.{mypatch,patch}; do
        if [[ -f "$patch_file" ]]; then
            _patch_filename=$(basename "$patch_file")
            source+=("staging-patches/$_patch_filename")
            sha256sums+=('SKIP')
        fi
    done
    shopt -u nullglob
fi

prepare() {
    # Get rid of old build dirs
    rm -rf build-{32,64}
    mkdir -p build-{32,64}

    # restore the wine tree to its git origin state, without wine-staging patches
    printf '%s\n' '  -> Cleaning wine source code tree...'
    git -C wine reset --hard HEAD  # restore tracked files
    git -C wine clean -xdf         # delete untracked files

    # apply all wine-staging patches
    printf '%s\n' '  -> Applying wine-staging patches...'
    wine-staging/staging/patchinstall.py DESTDIR="${srcdir}/wine" --all \
    --no-autoconf \
    -W eventfd_synchronization \
    -W server-PeekMessage \
    -W server-Signal_Thread \
    -W ws2_32-af_unix

    # Apply NTSYNC patch if enabled
    if [[ "$NTSYNC" -eq "1" ]]; then
      printf '%s\n' "======================NTSYNC==========================="
      patch -Np1 --verbose -d wine -i "$srcdir"/wine-ntsync.patch
      printf '%s\n' "======================================================="
    fi

    # Apply TKG patches
    _tkg_path="$srcdir"/wine-tkg-git/wine-tkg-git/wine-tkg-patches
    printf '%s\n' "  -> Applying TKG patches..."
    patch -Np1 -d wine -i "$_tkg_path"/proton/LAA/LAA-unix-staging.patch
    patch -Np1 -d wine -i "$_tkg_path"/proton/msvcrt_nativebuiltin/msvcrt_nativebuiltin_mainline.patch
    patch -Np1 -d wine -i "$_tkg_path"/proton/proton-win10-default/proton-win10-default.patch

    # Apply core patches
    printf '%s\n' "  -> Applying core patches..."
    patch -Np1 -d wine -i "$srcdir"/wine-wmclass.patch
    patch -Np1 -d wine -i "$srcdir"/wine-isolate_home.patch
    patch -Np1 -d wine -i "$srcdir"/wine-hags.patch

    # Apply MR patches if any exist
    if [[ ${#_mr_patches[@]} -gt 0 ]]; then
        printf '%s\n' "  -> Applying MR patches..."
        for mr in "${!_mr_patches[@]}"; do
            printf '%s\n' "============================================="
            printf '%s\n' "  -> Applying MR ${mr} patch: ${_mr_patches[${mr}]}"
            patch -Np1 -d wine -i "$_patch_dir/${mr}.patch"
        done
    else
        printf '%s\n' "  -> No MR patches to apply."
    fi

    # Apply all patches from staging-patches folder if enabled
    if [[ "$USE_STAGING_PATCHES" -eq 1 ]] && [[ -d "$srcdir/staging-patches" ]]; then
        printf '%s\n' "  -> Applying additional staging patches..."
        shopt -s nullglob
        for patch_file in "$srcdir/staging-patches"/*.{mypatch,patch}; do
            if [[ -f "$patch_file" ]]; then
                printf '%s\n' "  -> Applying $(basename "$patch_file")..."
                patch -Np1 -d wine -i "$patch_file"
            fi
        done
        shopt -u nullglob
    fi

    # Run wine build generation tools
    cd wine
    ./tools/make_requests
    ./dlls/winevulkan/make_vulkan
    ./tools/make_specfiles
    autoreconf -fiv
}

pkgver() {
    _pkgver=$(git -C wine describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^wine\.//;s/^v//;s/\.rc/rc/')
    if [[ "$NTSYNC" -eq "1" ]]; then
        echo "$_pkgver" | sed 's/g/ntsync.g/'
    else
        echo "$_pkgver"
    fi
}

build() {
    # GCC 15 specific flags and fixes
    # Address incompatible pointer types, implicit fallthrough, and GCC 15 specific warnings
    export CFLAGS="$CFLAGS -ffat-lto-objects -Wno-error=incompatible-pointer-types -Wno-error=implicit-fallthrough -Wno-error=maybe-uninitialized -fcommon"
    export CXXFLAGS="$CXXFLAGS -ffat-lto-objects -Wno-error=incompatible-pointer-types -Wno-error=implicit-fallthrough -Wno-error=maybe-uninitialized -fcommon"

    # apply flags for cross-compilation
    export CROSSCFLAGS="-O2 -pipe -fcommon"
    export CROSSCXXFLAGS="-O2 -pipe -fcommon"
    export CROSSLDFLAGS="-Wl,-O1"

    local -a split=($CFLAGS)
    local -A flags
    for opt in "${split[@]}"; do flags["${opt%%=*}"]="${opt##*=}"; done
    local march="${flags["-march"]:-znver4}"  # Updated for newer AMD CPUs
    local mtune="${flags["-mtune"]:-znver4}"

    # Optimization and compiler flags for GCC 15
    OPTIMIZE_FLAGS="-O3 -march=$march -mtune=$mtune -mfpmath=sse -pipe"
    SANITY_FLAGS="-fwrapv -fno-strict-aliasing -fcommon"
    WARNING_FLAGS="-Wno-incompatible-pointer-types -Wno-error=implicit-fallthrough -Wno-error=maybe-uninitialized"
    STRIP_FLAGS="-s"
    COMMON_FLAGS="$OPTIMIZE_FLAGS $SANITY_FLAGS $WARNING_FLAGS $STRIP_FLAGS"

    COMMON_LDFLAGS="-Wl,-O1,--sort-common,--as-needed"

    export LDFLAGS="$COMMON_LDFLAGS"
    export CROSSLDFLAGS="$COMMON_LDFLAGS -Wl,--file-alignment,4096"

    # Build in parallel using all available CPU cores
    export MAKEFLAGS="-j$(nproc)"

    # build wine-staging 64-bit
    # (according to the wine wiki, this 64-bit/32-bit building order is mandatory)
    printf '%s\n' '  -> Building wine-staging-64...'
    export CFLAGS="$COMMON_FLAGS -mcmodel=small"
    export CXXFLAGS="$COMMON_FLAGS -mcmodel=small -std=c++17"
    export CROSSCFLAGS="$COMMON_FLAGS -mcmodel=small"
    export CROSSCXXFLAGS="$COMMON_FLAGS -mcmodel=small -std=c++17"
    export PKG_CONFIG_PATH="/usr/lib/pkgconfig:/usr/share/pkgconfig"
    cd "${srcdir}/build-64"
    ../wine/configure \
        --prefix='/usr' \
        --libdir='/usr/lib' \
        --with-x \
        --with-wayland \
        --with-gstreamer \
        --with-mingw \
        --with-alsa \
        --without-oss \
        --disable-winemenubuilder \
        --with-xattr \
        --enable-win64 \
        --disable-tests
    make

    # build wine-staging 32-bit
    printf '%s\n' '  -> Building wine-staging-32...'
    export CFLAGS="$COMMON_FLAGS -mstackrealign"
    export CXXFLAGS="$COMMON_FLAGS -mstackrealign -std=c++17"
    export CROSSCFLAGS="$COMMON_FLAGS -mstackrealign"
    export CROSSCXXFLAGS="$COMMON_FLAGS -mstackrealign -std=c++17"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/share/pkgconfig"
    cd "${srcdir}/build-32"
    ../wine/configure \
        --prefix='/usr' \
        --libdir='/usr/lib' \
        --with-x \
        --with-wayland \
        --with-gstreamer \
        --with-mingw \
        --with-alsa \
        --without-oss \
        --disable-winemenubuilder \
        --with-xattr \
        --with-wine64="${srcdir}/build-64" \
        --disable-tests
    make
}

package() {
    # package wine-staging 32-bit
    # (according to the wine wiki, this reverse 32-bit/64-bit packaging order is important)
    printf '%s\n' '  -> Packaging wine-staging-32...'
    cd "${srcdir}/build-32"
    make prefix="${pkgdir}/usr" \
         libdir="${pkgdir}/usr/lib" \
         dlldir="${pkgdir}/usr/lib/wine" \
         install

    # package wine-staging 64-bit
    printf '%s\n' '  -> Packaging wine-staging-64...'
    cd "${srcdir}/build-64"
    make prefix="${pkgdir}/usr" \
         libdir="${pkgdir}/usr/lib" \
         dlldir="${pkgdir}/usr/lib/wine" \
         install

    # font aliasing settings for win32 applications
    install -d -m755 "${pkgdir}/usr/share/fontconfig/conf.default"
    install -D -m644 "${srcdir}/30-win32-aliases.conf" -t "${pkgdir}/usr/share/fontconfig/conf.avail"
    ln -s ../conf.avail/30-win32-aliases.conf "${pkgdir}/usr/share/fontconfig/conf.default/30-win32-aliases.conf"

    # wine binfmt
    install -D -m644 "${srcdir}/wine-binfmt.conf" "${pkgdir}/usr/lib/binfmt.d/wine.conf"

    # strip native PE libraries
    i686-w64-mingw32-strip --strip-unneeded "${pkgdir}/usr/lib/wine/i386-windows"/*.{dll,exe}
    x86_64-w64-mingw32-strip --strip-unneeded "${pkgdir}/usr/lib/wine/x86_64-windows"/*.{dll,exe}
}

# vim:set ts=4 sts=4 sw=4 et:
