pkgname=pyroveil
pkgver=r22.00a602f
pkgrel=1
pkgdesc='Vulkan layer to replace shaders or roundtrip them to workaround driver bugs'
arch=('x86_64')
license=('MIT')
url='https://github.com/HansKristian-Work/pyroveil'
makedepends=('cmake' 'ninja' 'git')
depends=('vulkan-icd-loader')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare(){
    cd ${pkgname}
    git submodule update --init
    #patch -Np1 <"../patchname.patch"
}

build() {
    cd ${pkgname}
    cmake . -Bbuild -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja -C build
}

package() {
    cd ${pkgname}
    DESTDIR="$pkgdir" ninja -C build install
}
