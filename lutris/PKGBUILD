# Maintainer: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Victor Fuentes <hyruleterminatriforce@gmail.com>
# Contributor: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Michael DeGuzis <mdeguzis@gmail.com>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
pkgname=lutris
pkgver=0.5.19.r19.g2f1c57e
pkgrel=1
pkgdesc="Open Gaming Platform"
arch=('any')
url="https://lutris.net"
license=('GPL-3.0-only')
depends=(
  cabextract
  curl
  gdk-pixbuf2
  glib2
  gnome-desktop
  gobject-introspection-runtime
  gtk3
  hicolor-icon-theme
  mesa-utils
  p7zip
  pango
  psmisc
  python
  python-cairo
  python-certifi
  python-dbus
  python-distro
  python-evdev
  python-gobject
  python-lxml
  python-moddb
  python-pillow
  python-requests
  python-urllib3
  python-yaml
  unzip
  webkit2gtk-4.1
  xdg-desktop-portal-impl
  xorg-xrandr
  python-defusedxml
)
makedepends=(
  git
  meson
  ninja
  protobuf
  python-poetry-core
  #icoutils
)
checkdepends=(
  python-nose
  xorg-server-xvfb
  xterm
  fluidsynth
  vulkan-tools
  pciutils
)
optdepends=(
  'fluidsynth: For games that have MIDI music'
  'gamemode: Allows games to request a temporary set of optimisations'
  'gvfs: GVFS backend'
  'innoextract: Extract Inno Setup installers'
  'libayatana-appindicator: tray icon support'
  'lib32-gamemode: Allows games to request a temporary set of optimisations'
  'lib32-vkd3d: DirectX 12 support'
  'lib32-vulkan-icd-loader: Vulkan support'
  'python-pefile: Display icons of Microsoft executable files'
  'python-protobuf: Battle.net support'
  'vkd3d: DirectX 12 support'
  'vulkan-icd-loader: Vulkan support'
  'vulkan-tools: Vulkan support'
  'wine: Windows support'
  'xorg-xgamma: Restore gamma on game exit'
  'umu-launcher: For running games through Proton'

)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" "wrestool" "icoutils")
source=('git+https://github.com/Zebra2711/lutris.git'
        'git+https://github.com/bartok765/galaxy_blizzard_plugin.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {

  # Regenerate protos to fix BattleNet plugin
  # See https://gitlab.archlinux.org/archlinux/packaging/packages/lutris/-/issues/4
  # And https://github.com/lutris/lutris/issues/5659
  protoc --proto_path=galaxy_blizzard_plugin/src --python_out=. product_db.proto
  cp -vf product_db_pb2.py "${pkgname%-git}"/lutris/util/battlenet/product_db_pb2.py
}

build() {
  arch-meson lutris build
  ninja -C build -j$(nproc)
}

check() {
  cd lutris
  xvfb-run nosetests
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  # Remove Python cache files and directories
  find "$pkgdir" -type d -name "__pycache__" -delete 2>/dev/null || true
  find "$pkgdir" -name "*.pyc" -delete
  #cp -vf /usr/bin/wrestool "${pkgdir}/usr/bin/wrestool"
}
